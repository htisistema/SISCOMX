* CONSULTA, ALTERACAO, EXCLUSAO NO RELOGIO DE PONTOS
********************************
FUNCTION sacrl1(mtp)
********************
LOCAL MPRG:='SACRL1',li:=18,ci:=05,lb:=21,cb:=58,;
      opcao,i:=0,lba:=16,cba:=58,m_dados:={},m_ponto:={},;
      point:=0,mhora,mdata

PUBLIC senha_acess,nivel_acess,cod_operado,mcod_oper:=0
IF ! ver_nivel(mprg,'CONSULTA, ALTERACAO, EXCLUSAO DO RELOGIO DE PONTO','1',nivel_acess,,'AMBIE')
        RETURN NIL
ENDIF
*-----------------------------------------------------
CLOSE ALL
IF ! AbriArq('sacrl','rl');RETURN NIL;ENDIF
IF ! AbriArq('insopera','sen');RETURN NIL;ENDIF
*-----------------------------------------------------
op_tela(03,05,21,58,' CONSULTA, ALTERACAO, EXCLUSAO DO RELOGIO DE PONTO ',,'*')
WHILE .T.
        exibi_prg(mprg)
        limpa(00,00,30,120)
        /*
        *************
        SELE('sen');ORDSETFOCUS(1)
        GO TOP
        *************
        */
        ASIZE(m_dados,0)
        ASIZE(m_ponto,0)
        mdata:= mdata_sis
        mensagem('Digite o Codigo do Operador e Senha')
        @ 3,1 TO 3,cba-1
        @ 5,1 TO 5,cba-1
        setcor(1)
        DEVPOS(1,1);DEVOUT('Codigo Operador:')
        DEVPOS(2,1);DEVOUT('DATA do Sistema:')
        DEVPOS(4,1);DEVOUT('Operador   Data       Hora')
        @ 1,18 GET mcod_oper PICT '999' VALID IF(EMPTY(mcod_oper),.F.,ven(mcod_oper,1,22))
        @ 2,18 GET mdata VALID IF(EMPTY(mdata),.F.,.T.)
        READ
        IF LASTKEY() = 27
                EXIT
        ENDIF
        q_cons := {}
        sr_getconnection():exec("SELECT * FROM sacrl WHERE cod_oper = "+sr_cdbvalue(STRZERO(mcod_oper,3))+' AND data = '+sr_cdbvalue(mdata),,.t.,@q_cons)
        sr_getconnection():exec('COMMIT',,.f.)
        IF LEN(q_cons) = 0
                atencao('Nao existe lancamento deste operador...')
                LOOP
        ENDIF
        setcor(3)
        i:=0
        FOR i = 1 TO LEN(q_cons)
                AADD(m_dados,'  '+q_cons[i,1]+'    '+DTOC(q_cons[i,2])+'    '+q_cons[i,3])
                AADD(m_ponto,q_cons[i,4])
        NEXT
        mensagem('<ESC> Retorna  -  <ENTER> p/Confirmar')
        setcor(10);point := ACHOICE(6,1,lba-1,cba-1,m_dados,,,point)
        setcor(1)
        DO CASE
                CASE LASTKEY()=27
                        EXIT
                CASE LASTKEY() = 13
                        setcor(1)
                        IF mtp = 'E'
                                opcao := mensagem1('Confirma a Exclusao:','N','S,N',lba)
                                IF opcao = 'N' .OR. LASTKEY() = 27
                                        LOOP
                                ENDIF
                                sr_getconnection():exec("DELETE FROM sacrl WHERE SR_RECNO = "+sr_cdbvalue(m_ponto[point]),,.f.)
                                sr_getconnection():exec("COMMIT",,.f.)
                        ELSEIF mtp = 'A'
                                mhora := q_cons[point,3]
                                mdata := q_cons[point,2]
                                botao(li,ci,lb,cb,,' ALTERACAO DE LANCAMENTO ','*')
                                @ li+1,ci+1 TO li+1,cb-1
                                DEVPOS(li+2,ci+1);DEVOUT('Data:              Hora:')
                                @ li+2,ci+7 GET mdata
                                @ li+2,ci+26 GET mhora PICT '99:99:99'
                                READ
                                opcao := mensagem1('Confirma a Alteracao:','N','S,N',lba)
                                IF opcao = 'N' .OR. LASTKEY() = 27
                                        LOOP
                                ENDIF
                                sr_getconnection():exec('UPDATE sacrl SET hora = '+sr_cdbvalue(mhora)+', data = '+sr_cdbvalue(mdata)+' WHERE SR_RECNO = ' + sr_cdbvalue(m_ponto[point]),,.f.)
                                sr_getconnection():exec("COMMIT",,.f.)
                        ENDIF
        ENDCASE
ENDDO
CLOSE sen
wvw_lclosewindow()
RETURN NIL
********************************** f i m **************************************
