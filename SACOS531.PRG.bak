**********************************
*LISTAGEM MOVIMENTO O.S. RESUMO PRODUTOS EM ABERTO
*********************************

FUNCTION sacos531
**************
LOCAL MPRG:='SACOS531',mtitulo:='RELATORIO DE PRODUTO DAS O.S. EM ABERTO (RESUMO)'

LOCAL tela,lci,cci,lba,cba,opcao,mdata1,mdata2,;
      mtot_sai,mvlr_sai,mcodigo,mtotg_sai,mvlrg_sai,;
      mgrupo,msub_grupo,mcod_for,mcod_cli,;
      mcod_ope:=0,mcod_espe:=0,msele,morde,mpoint:=0

PRIVATE mtraco,mtit,mtipo,mpag,mposicao,mcod_merc:=0,cons_cli := {}

PRIVATE mtipo_imp,mimp_tipo:=0,marq:=SPACE(35)

IF ! ver_nivel(mprg,mtitulo,'15',nivel_acess,,'AMBIE')
        RETURN NIL
ENDIF

mtraco := REPLI('=',137)

lci := cci := 00
lba := 10
cba := 70
*----------------------------------------------------------------------------
CLOSE ALL
IF ! AbriArq('insopera','sen');RETURN NIL;ENDIF
IF ! AbriArq('sacespe','espe');RETURN NIL;ENDIF
//
IF ! AbriArq('sacospro','ospro');RETURN NIL;ENDIF
IF ! AbriArq('SACCLI','CLI');RETURN NIL;ENDIF
IF ! AbriArq('sacoss','oss');RETURN NIL;ENDIF
IF ! AbriArq('sacforn','forn');RETURN NIL;ENDIF
IF ! AbriArq('sacgrupo','grup');RETURN NIL;ENDIF
IF ! AbriArq('sacmerc','merc');RETURN NIL;ENDIF
*----------------------------------------------------------------------------
mtipo_imp := m_indiv[1,18]
op_tela(lci,cci,lba+2,cba,mtitulo)

WHILE .T.
        WHILE .T.
                ERASE SACMOVCN.CDX
                IF ! AbriArq('sacmovcn','movcn','E');RETURN NIL;ENDIF
                ************************
                SELE('movcn')
                ************************
                IF ! NETERR()
                        ZAP
                        EXIT
                ENDIF
        ENDDO
        mdata1 := mdata_sis - 30
        mdata2 := mdata_sis
        mgrupo := mcod_ope := mcod_for := mcod_cli := ;
        msub_grupo := mcod_merc := 0
        mensagem('Preencha os Campos - <ESC>p/Abandonar')
        DEVPOS(lci+1,cci+2);DEVOUT('Data Inicial.....:')
        DEVPOS(lci+2,cci+2);DEVOUT('Data Final.......:')
        DEVPOS(lci+3,cci+2);DEVOUT('Codigo PRODUTO...:')
        DEVPOS(lci+4,cci+2);DEVOUT('Codigo GRUPO.....:')
        DEVPOS(lci+5,cci+2);DEVOUT('Codigo SUB-GRUPO.:')
        DEVPOS(lci+6,cci+2);DEVOUT('Codigo OPERADOR..:')
        DEVPOS(lci+7,cci+2);DEVOUT('Codigo FORNECEDOR:')
        DEVPOS(lci+8,cci+2);DEVOUT('Codigo CLIENTE...:')
        DEVPOS(lci+9,cci+2);DEVOUT('Cod.da Especie Produto............:')
        @ lci+1,cci+21 GET mdata1 PICT '99/99/99' VALID IF(EMPTY(mdata1),.F.,.T.)
        @ lci+2,cci+21 GET mdata2 PICT '99/99/99' VALID IF(mdata2 < mdata1 ,.F.,.T.)
        @ lci+3,cci+21 GET mcod_merc PICT '99999' VALID ver_cod(mcod_merc,lci+3,cci+27,,0)
        READ
        IF LASTKEY() = 27
                wvw_lclosewindow()
                RETURN NIL
        ENDIF
        ver_cod(mcod_merc,lci+3,cci+27,,0)
        @ lci+4,cci+21 GET mgrupo PICT '999' VALID IF(EMPTY(mgrupo),.T.,ver_gru(@mgrupo,lci+4,cci+26,.F.))
        @ lci+5,cci+21 GET msub_grupo PICT '99' WHEN ! EMPTY(mgrupo) VALID ver_sgru(mgrupo,msub_grupo,lci+5,cci+26)
        @ lci+6,cci+21 GET mcod_ope PICT '999' VALID IF(EMPTY(mcod_ope),.T.,ven(mcod_ope,lci+6,cci+26))
        @ lci+7,cci+21 GET mcod_for PICT '9999' VALID IF(EMPTY(mcod_for),.T.,v_fornece(mcod_for,lci+7,cci+26))
        @ lci+8,cci+21 GET mcod_cli PICT '99999' VALID ver_cli(mcod_cli,lci+8,cci+27)
        @ lci+9,cci+38 GET mcod_espe PICT '9999' VALID ver_espe(mcod_espe,lci+9,cci+40,,0)
        READ
        IF LASTKEY() = 27
                wvw_lclosewindow()
                RETURN NIL
        ENDIF
        mensagem('Aguarde um momento coletando dados p/o relatorio')
        ****************
        SELE('oss');ORDSETFOCUS(2)
        GO TOP
        ****************
        oss->(DBSEEK(DTOS(mdata1),.T.))
        IF EOF()
                atencao('Nao existe movimento neste periodo !!!')
                LOOP
        ENDIF
        WHILE oss->data_ent <= mdata2 .AND. ! EOF()
                prog_imp(RECNO())
                IF  (! EMPTY(mcod_ope) .AND. STRZERO(mcod_ope,3) <> oss->operador);
                   .OR. (! EMPTY(mcod_cli) .AND. oss->cod_cli<>mcod_cli);
                   .OR. (! EMPTY(oss->baixa));
                   .OR. (! EMPTY(oss->pag))
                        SKIP;LOOP
                ENDIF
                ****************
                SELE('ospro');ORDSETFOCUS(1)
                GO TOP
                ****************
                IF ospro->(DBSEEK(STRZERO(VAL(oss->num_os),6)))
                        WHILE ospro->num_os = oss->num_os .AND. ! EOF()
                                prog_imp(RECNO())
                                ver_cod(VAL(ospro->cod_merc),,,,,'*')
                                IF (! EMPTY(mgrupo) .AND. STRZERO(mgrupo,3) <> SUBSTR(merc->gru_sub,1,3));
                                   .OR. (! EMPTY(mcod_merc) .AND. mcod_merc <> ospro->cod_merc);
                                   .OR. (! EMPTY(msub_grupo) .AND. STRZERO(mgrupo,3)+STRZERO(msub_grupo,2) <> merc->gru_sub);
                                   .OR. (! EMPTY(mcod_for) .AND. merc->cod_fab <> STRZERO(mcod_for,4));
                                   .OR. (! EMPTY(mcod_espe) .AND. merc->especie <> STRZERO(mcod_espe,4))
                                        SKIP;LOOP
                                ENDIF
                                mpoint := RECNO()
                                *****************************
                                SELE('movcn')
                                *****************************
                                IF ! ADIREG()
                                        atencao('Nao foi possivel adicionar o registro')
                                        LOOP
                                ENDIF
                                movcn->data_s_e := oss->data_ent
                                movcn->codigo   := ospro->cod_merc
                                movcn->produto  := ospro->merc
                                movcn->quantd   := ospro->quantd
                                movcn->vl_fatura:= ospro->vlr_fat
                                ***************************
                                SELE('ospro');ORDSETFOCUS(1)
                                ***************************
                                GO mpoint
                                SKIP
                        ENDDO
                ENDIF
                ***************************
                SELE('oss');ORDSETFOCUS(2)
                ***************************
                SKIP
        ENDDO
        mpag := mtot_sai := mtotg_sai := mvlr_sai := mvlrg_sai := 0
        mtit = 'Listagem de O.S. em ABERTO (RESUMO GERAL)'
        IF ! EMPTY(mcod_ope)
                mtit = mtit+' - Operador:'+STRZERO(mcod_ope,3)
        ENDIF
        mtipo = 'De '+DTOC(mdata1)+' ate '+DTOC(mdata2)
        marq := 'RES_MOV.REL'
        ******************************
        ERASE SACMOVCN.CDX
        SELE('movcn')
        INDEX ON movcn->codigo+DTOS(movcn->data_s_e) TAG cod TO ('sacmovcn')
        prog(24,50,50)
        INDEX ON movcn->produto+movcn->codigo TAG prod TO ('sacmovcn')
        prog(24,50,100)
        ORDSETFOCUS(2)
        GO TOP
        ******************************
        IF ! imp_arq('RES_MOV.REL','R')
                LOOP
        ENDIF
        WHILE ! EOF()
                IF mpag = 0 .OR. PROW() >= 59
                        mpag ++
                        IF mpag > 1
                                EJECT
                        ENDIF
                        cabecalho(mpag,mtit,mtipo,mprg)
                        imprt(mtipo_imp,'C')
                        DEVPOS(PROW()+1,00);DEVOUT('Codigo')
                        DEVPOS(PROW(),49);DEVOUT('Qtd.Saidas')
                        DEVPOS(PROW(),PCOL()+2);DEVOUT('  Vlr.Vendas')
                        DEVPOS(PROW()+1,00);DEVOUT(mtraco)
                ENDIF
                mcodigo := movcn->codigo
*               ver_cod(VAL(movcn->codigo),,,,0,'*')
                DEVPOS(PROW()+1,00);DEVOUT(movcn->codigo)
                DEVPOS(PROW(),PCOL()+1);DEVOUT(movcn->produto)
                WHILE mcodigo = movcn->codigo .AND. ! EOF()
                        mvlr_sai = mvlr_sai + (movcn->vl_fatura * movcn->quantd)
                        mtot_sai = mtot_sai + movcn->quantd
                        mvlrg_sai = mvlrg_sai + (movcn->vl_fatura * movcn->quantd)
                        mtotg_sai = mtotg_sai + movcn->quantd
                        SKIP
                ENDDO
                DEVPOS(PROW(),49);DEVOUTPICT(mtot_sai,'999,999.99')
                DEVPOS(PROW(),PCOL()+2);DEVOUTPICT(mvlr_sai,'9,999,999.99')
                mposicao := RECNO()
                msele := SELE()
                morde := INDEXORD()
                mtot_sai := mvlr_sai := 0
                SELE(msele);IF(morde>0,ORDSETFOCUS(morde),)
                GO mposicao
        ENDDO
        IF PROW() > 53
                mpag ++
                EJECT
                cabecalho(mpag,mtit,mtipo,mprg)
        ENDIF
        imprt(mtipo_imp,'C')
        DEVPOS(PROW()+1,49);DEVOUT('----------')
        DEVPOS(PROW(),PCOL()+2);DEVOUT('------------')
        DEVPOS(PROW()+1,00);DEVOUT('R E S U M O   G E R A L:')
        DEVPOS(PROW(),47);DEVOUT(TRANSFORM(mtotg_sai,'9,999,999.99'))
        DEVPOS(PROW(),PCOL()+1);DEVOUT(TRANSFORM(mvlrg_sai,'99,999,999.99'))
        DEVPOS(PROW(),00);DEVOUT(TIME())
        EJECT
        SETPRC(00,00)
        SET DEVI TO SCREEN;SET PRINT TO;SET PRINT OFF
        conf_impressao(marq)
        movcn->(DBCLOSEAREA())
ENDDO
wvw_lclosewindow()
RETURN NIL
*************************** F I M ************************

